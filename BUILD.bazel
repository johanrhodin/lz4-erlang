load("@rules_erlang//:erlang_app.bzl", "erlang_app", "test_erlang_app")
load("@rules_erlang//:xref2.bzl", "xref")
load("@rules_erlang//:dialyze.bzl", "dialyze", "plt")
load("@rules_erlang//:ct.bzl", "assert_suites", "ct_suite")

APP_NAME = "lz4"

APP_DESCRIPTION = "An LZ4 compression library that wraps an NIF"

APP_VERSION = "1.9.2.3"

DEPS = [
    "@host_triple//:erlang_app",
]

cc_binary(
    name = "lz4_nif_linux",
    srcs = glob([
        "c_src/**/*.c",
        "c_src/**/*.h",
    ]) + [
        "@rules_erlang//tools:erlang_headers",
        "@lz4_src//:lib/lz4.h",
        "@lz4_src//:lib/lz4frame.h",
        "@lz4_src//:static_library_linux",
        "@nif_helpers//:nif_helpers.h",
        "@nif_helpers//:nif_helpers.c",
    ],
    copts = [
        "-I $(BINDIR)/external/rules_erlang.3.2.0/tools/erlang_headers",
        "-I external/.external_deps.lz4_src/lib",
        "-I external/.external_deps.nif_helpers",
        "-fPIC",
        # "-Wno-implicit-function-declaration",
        # "-Wno-unused-command-line-argument",
        # "-v",
    ],
    exec_compatible_with = [
        "@bazel_tools//platforms:x86_64",
        "@bazel_tools//platforms:linux",
        "@bazel_tools//tools/cpp:clang",
    ],
    linkshared = True,
    target_compatible_with = [
        "@bazel_tools//platforms:x86_64",
        "@bazel_tools//platforms:linux",
    ],
)

genrule(
    name = "lz4_nif_so",
    srcs = [":lz4_nif_linux"],
    outs = ["priv/lz4_nif.so"],
    cmd = "cp $< $@",
)

erlang_app(
    app_description = APP_DESCRIPTION,
    app_name = APP_NAME,
    app_version = APP_VERSION,
    extra_priv = ["priv/lz4_nif.so"],
    deps = DEPS,
)

test_erlang_app(
    app_description = APP_DESCRIPTION,
    app_name = APP_NAME,
    app_version = APP_VERSION,
    extra_priv = ["priv/lz4_nif.so"],
    deps = DEPS,
)

xref()

plt(
    name = "base_plt",
)

dialyze(
    size = "small",
    plt = ":base_plt",
)

genrule(
    name = "place_reference_file",
    srcs = ["@pdf_reference//file"],
    outs = ["test/lz4f_SUITE_data/pdf_reference_1-7.pdf"],
    cmd = "cp $< $@",
)

assert_suites([
    ct_suite(
        name = "lz4f_SUITE",
        data = [
            "test/lz4f_SUITE_data/pdf_reference_1-7.pdf",
        ],
        runtime_deps = [
            "@ct_helper//:erlang_app",
        ],
    ),
])
